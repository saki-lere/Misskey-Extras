/// @ 0.12.4
### {
  name: "Catify"
  version: "1.3.6 (AS0.12.4版)"
  author:  "@salano_ym (Original: volpeon)"
  description: "Various features to make your Misskey experience even more catty"
  permission: []
  config: {
    users_to_cats: {
      type: "boolean"
      label: "Turn all users into cats"
      description: "Turns all users into cats wherever possible."
      default: true
    }
    nyanify_notes: {
      type: "boolean"
      label: "Nyanify outgoing posts"
      description: "Nyanifies your posts on sending."
      default: true
    }
    nyanify_extra: {
      type: "boolean"
      label: "Nyanify extra"
      description: "Apply a stronger nyanification filter than Misskey's."
      default: true
    }
    nyanify_suffix: {
      type: "string"
      label: "Sentence suffix"
      description: "End sentences with this suffix. An empty suffix disables this feature."
      default: ", nya"
    }
    nyanify_suffix_freq: {
      type: "number"
      label: "Sentence suffix frequency"
      description: "Apply the suffix every nth sentence."
      default: 1
    }
  }
}

:: Util {
  @pipe(els) {
    els.reduce(@(res, el) { el(res) })
  }
}

:: MutableTrie {
  @empty() {
    {
      value: null
      children: {}
    }
  }

  @has(trie, c) {
    Obj:has(trie.children, c)
  }

  @get(trie, c) {
    Obj:get(trie.children, c)
  }

  @insert(trie, key, value) {
    var t = trie

    each let c, key.split() {
      if !(has(t, c)) {
        Obj:set(t.children, c, empty())
      }

      t = get(t, c)
    }

    t.value = value
  }
}

:: MutableStream {
  @of(str) {
    let chars = str.split()
    {
      chars: chars
      len: chars.len
      pos: 0
      state: {}
    }
  }

  @end(stream) {
    (stream.pos >= stream.len)
  }

  @peek(stream) {
    if end(stream) {
      return null
    }

    let chars = stream.chars
    chars[stream.pos]
  }

  @take(stream) {
    let c = peek(stream)

    if (c != null) {
      stream.pos += 1
    }

    c
  }
}

@takeWhile(stream, cond, out) {
  let c = MutableStream:peek(stream)

  if (c != null) {
    if cond(c) {
      out.push(c)
      stream.pos += 1
      takeWhile(stream, cond, out)
    }
  }
}

@takeUntil(stream, cond, out) {
  takeWhile(stream, @(c) { !(cond(c)) }, out)
}

@match_(trie, stream) {
  var tr = trie
  var path = []
  var successfulPath = ""
  var pos = stream.pos
  var action = trie.value

  @run() {
    let c = MutableStream:take(stream)

    if (c != null) {
      path.push(c)

      if MutableTrie:has(tr, c) {
        tr = MutableTrie:get(tr, c)
        if (tr.value != null) {
          successfulPath = path.join()
          pos = stream.pos
          action = tr.value
        }
        run()
      }
    }
  }

  run()

  stream.pos = pos

  {
    path: successfulPath
    value: action
  }
}

@actionContinue() {
  {
    type: "continue"
  }
}

@actionNoNyanification() {
  {
    type: "noNyanification"
  }
}

@actionReplace(str) {
  {
    type: "replace"
    str: str
  }
}

@actionReplaceWhole(str) {
  {
    type: "replaceWhole"
    str: str
  }
}

@actionSkipUntilChar(c) {
  {
    type: "skipUntilChar"
    c: c
  }
}

@actionSkipWordBoundary() {
  {
    type: "skipWordBoundary"
  }
}

@actionSkipUntilSpace() {
  {
    type: "skipUntilSpace"
  }
}

@actionSuffix(str) {
  {
    type: "suffix"
    str: str
  }
}

let tr = MutableTrie:empty()

MutableTrie:insert(tr, "", actionContinue())

MutableTrie:insert(tr, "(nya=off)", actionNoNyanification())

MutableTrie:insert(tr, ":", actionSkipUntilChar(":"))
MutableTrie:insert(tr, ": ", actionContinue())
MutableTrie:insert(tr, `:{Str:lf}`, actionContinue())

MutableTrie:insert(tr, "`", actionSkipUntilChar("`"))

MutableTrie:insert(tr, "http://", actionSkipUntilSpace())
MutableTrie:insert(tr, "https://", actionSkipUntilSpace())
MutableTrie:insert(tr, "@", actionSkipUntilSpace())

MutableTrie:insert(tr, "$[", actionSkipUntilSpace())

MutableTrie:insert(tr, "な", actionReplace("にゃ"))
MutableTrie:insert(tr, "ナ", actionReplace("ニャ"))
MutableTrie:insert(tr, "ﾅ", actionReplace("ﾆｬ"))

MutableTrie:insert(tr, "na", actionReplace("nya"))
MutableTrie:insert(tr, "Na", actionReplace("Nya"))
MutableTrie:insert(tr, "NA", actionReplace("NYA"))

MutableTrie:insert(tr, "morning", actionReplace("mornyan"))
MutableTrie:insert(tr, "Morning", actionReplace("Mornyan"))
MutableTrie:insert(tr, "MORNING", actionReplace("MORNYAN"))

MutableTrie:insert(tr, "everyone", actionReplace("everynyan"))
MutableTrie:insert(tr, "Everyone", actionReplace("Everynyan"))
MutableTrie:insert(tr, "EVERYONE", actionReplace("EVERYNYAN"))

if Plugin:config.nyanify_extra {
  MutableTrie:insert(tr, "nu", actionReplace("nyu"))
  MutableTrie:insert(tr, "Nu", actionReplace("Nyu"))
  MutableTrie:insert(tr, "NU", actionReplace("NYU"))

  MutableTrie:insert(tr, "hello", actionReplaceWhole("henlo"))
  MutableTrie:insert(tr, "Hello", actionReplaceWhole("Henlo"))
  MutableTrie:insert(tr, "HELLO", actionReplaceWhole("HENLO"))

  MutableTrie:insert(tr, "hand", actionReplaceWhole("paw"))
  MutableTrie:insert(tr, "Hand", actionReplaceWhole("Paw"))
  MutableTrie:insert(tr, "HAND", actionReplaceWhole("PAW"))

  MutableTrie:insert(tr, "hands", actionReplaceWhole("paws"))
  MutableTrie:insert(tr, "Hands", actionReplaceWhole("Paws"))
  MutableTrie:insert(tr, "HANDS", actionReplaceWhole("PAWS"))

  MutableTrie:insert(tr, "foot", actionReplaceWhole("paw"))
  MutableTrie:insert(tr, "Foot", actionReplaceWhole("paw"))
  MutableTrie:insert(tr, "FOOT", actionReplaceWhole("PAW"))

  MutableTrie:insert(tr, "feet", actionReplaceWhole("paws"))
  MutableTrie:insert(tr, "Feet", actionReplaceWhole("paws"))
  MutableTrie:insert(tr, "FEET", actionReplaceWhole("PAWS"))

  MutableTrie:insert(tr, "arm", actionReplaceWhole("foreleg"))
  MutableTrie:insert(tr, "Arm", actionReplaceWhole("Foreleg"))
  MutableTrie:insert(tr, "ARM", actionReplaceWhole("FORELEG"))

  MutableTrie:insert(tr, "leg", actionReplaceWhole("hindleg"))
  MutableTrie:insert(tr, "Leg", actionReplaceWhole("Hindleg"))
  MutableTrie:insert(tr, "LEG", actionReplaceWhole("HINDLEG"))

  MutableTrie:insert(tr, "now", actionReplaceWhole("nyow"))
  MutableTrie:insert(tr, "Now", actionReplaceWhole("Nyow"))
  MutableTrie:insert(tr, "NOW", actionReplaceWhole("NYOW"))
}

let nyaSuffix = Plugin:config.nyanify_suffix
let nyaSuffixFreq = Plugin:config.nyanify_suffix_freq

if (nyaSuffix != "") {
  MutableTrie:insert(tr, ".", actionSuffix(nyaSuffix))
  MutableTrie:insert(tr, ";", actionSuffix(nyaSuffix))
  MutableTrie:insert(tr, "!", actionSuffix(nyaSuffix))
  MutableTrie:insert(tr, "?", actionSuffix(nyaSuffix))
  MutableTrie:insert(tr, `{nyaSuffix}.`, actionSkipWordBoundary())
  MutableTrie:insert(tr, `{nyaSuffix};`, actionSkipWordBoundary())
  MutableTrie:insert(tr, `{nyaSuffix}!`, actionSkipWordBoundary())
  MutableTrie:insert(tr, `{nyaSuffix}?`, actionSkipWordBoundary())
}

@constYes() {
  true
}

@isSpace(c) {
  if (c == null) { true }
  elif (c == " ") { true }
  elif (c == Str:lf) { true }
  else { false }
}

@isWordBoundary(c) {
  if isSpace(c) { true }
  elif (c == ".") { true }
  elif (c == ",") { true }
  elif (c == ";") { true }
  elif (c == "!") { true }
  elif (c == "?") { true }
  else { false }
}

@isCandidate(c) {
  MutableTrie:has(tr, c)
}

@parse(stream, out, sentenceNo) {
  takeUntil(stream, isCandidate, out)

  if MutableStream:end(stream) {
    return out.join()
  }

  let res = match_(tr, stream)
  let path = res.path
  let action = res.value
  var sentence = sentenceNo

  match action.type {
    "continue" => {
      if (path == "") {
        out.push(MutableStream:take(stream))
      } else {
        out.push(path)
      }
    }

    "noNyanification" => {
      out.push(path)
      takeWhile(stream, constYes, out)
    }

    "replace" => {
      out.push(action.str)
    }

    "replaceWhole" => {
      let len = out.len
      let last = if (len == 0) { null } else { out[len-1] }
      let next = MutableStream:peek(stream)
      if (isWordBoundary(last) && isWordBoundary(next)) {
        out.push(action.str)
      } else {
        out.push(path)
      }
    }

    "skipUntilChar" => {
      out.push(path)
      takeWhile(stream, @(c) { (c != action.c) }, out)
      out.push(MutableStream:take(stream))
    }

    "skipUntilSpace" => {
      out.push(path)
      takeUntil(stream, isSpace, out)
    }

    "skipWordBoundary" => {
      out.push(path)
      takeWhile(stream, isWordBoundary, out)
    }

    "suffix" => {
      let len = out.len
      let last = if (len == 0) { null } else { out[len-1] }
      let next = MutableStream:peek(stream)
      if (!(isWordBoundary(last)) && isWordBoundary(next)) {
        if ((sentence % nyaSuffixFreq) == 0) {
          out.push(action.str)
        }
        sentence += 1
        out.push(path)
        takeWhile(stream, isWordBoundary, out)
      } else {
        out.push(path)
      }
    }
  }

  parse(stream, out, sentence)
}

@nyanify(text) {
  parse(MutableStream:of(text), [], nyaSuffixFreq)
}

@nyanifyNoteText(note) {
  if ((note.text == null) || (note.text == "")) {
    return note
  }

  note.text = nyanify(note.text)

  note
}

@catifyNote(note) {
  note.user.isCat = true
  note = nyanifyNoteText(note)

  if (note.renote != null) {
    note.renote = catifyNote(note.renote)
  }

  note
}

if Plugin:config.users_to_cats {
  Plugin:register_note_view_interruptor(catifyNote)
}

if Plugin:config.nyanify_notes {
  Plugin:register_note_post_interruptor(nyanifyNoteText)
}

